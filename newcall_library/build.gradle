apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.kezong.fat-aar'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 30
        versionCode 209
        versionName "2.0.9"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }
    lintOptions {
        abortOnError false
    }

    defaultPublishConfig "SpreadtrumRelease"
    flavorDimensions "vendor"
    productFlavors {
        QualcommZTE {
            dimension "vendor"
            buildConfigField("String", "ACTION_NAME", "\"com.cmcc.action.ims.datachannel\"")
            buildConfigField("String", "PACKAGE_NAME", "\"com.android.dialer\"")//provide by ZTE
            buildConfigField("long", "CHUNK_SIZE", "64000")  // rfc8864 sdp协商 max-message-size 默认为64k
        }

        QualcommMi {
            dimension "vendor"
            buildConfigField("String", "ACTION_NAME", "\"com.cmcc.action.ims.datachannel\"")
            buildConfigField("String", "PACKAGE_NAME", "\"com.android.incallui\"")//provide by Xiaomi
            buildConfigField("long", "CHUNK_SIZE", "64000")  // rfc8864 sdp协商 max-message-size 默认为64k
        }

        Qualcomm {
            dimension "vendor"
            buildConfigField("String", "ACTION_NAME", "\"com.cmcc.action.ims.datachannel\"")
            buildConfigField("String", "PACKAGE_NAME", "\"com.cmcc.dcservice\"")//default, vivo use
            buildConfigField("long", "CHUNK_SIZE", "64000")  // rfc8864 sdp协商 max-message-size 默认为64k
        }

        Mtk {
            dimension "vendor"
            buildConfigField("String", "ACTION_NAME", "\"BIND_SERVICE\"")
            buildConfigField("String", "PACKAGE_NAME", "\"com.cmcc.dcservice\"")
            buildConfigField("long", "CHUNK_SIZE", "64000") // rfc8864 sdp协商 max-message-size 默认为64k
        }

        Samsung {
            dimension "vendor"
            buildConfigField("String", "ACTION_NAME", "\"com.shannon.imsservice.call.dc.IImsDataChannelServiceController\"")
            buildConfigField("String", "PACKAGE_NAME", "\"com.shannon.imsservice\"")
            buildConfigField("long", "CHUNK_SIZE", "64000")  // rfc8864 sdp协商 max-message-size 默认为64k
        }

        Spreadtrum {
            dimension "vendor"
            buildConfigField("String", "ACTION_NAME", "\"com.unisoc.ims.ImsDataChannelService\"")
            buildConfigField("String", "PACKAGE_NAME", "\"com.spreadtrum.ims\"")
            buildConfigField("long", "CHUNK_SIZE", "64000")  // rfc8864 sdp协商 max-message-size 默认为64k
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //for Android Test case.
    packagingOptions {
        exclude 'org/apache/commons/codec/language/*'
        exclude 'org/apache/commons/codec/language/bm/*'
    }

}

fataar {
    /**
     * If transitive is true, local jar module and remote library's dependencies will be embed. (local aar module does not support)
     * If transitive is false, just embed first level dependency
     * Default value is false
     * @since 1.3.0
     */
    transitive = false
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    //
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    // room
    api "androidx.room:room-runtime:2.4.2"
    annotationProcessor "androidx.room:room-compiler:2.4.2"
    kapt("androidx.room:room-compiler:2.4.2")
    // gson
    implementation 'com.google.code.gson:gson:2.8.6'
    // bolts
    implementation 'com.parse.bolts:bolts-tasks:1.4.0'
    // okio
    implementation "com.squareup.okio:okio:2.8.0"
    // dc aidl接口
    embed (name: 'newcall_dc_aidl-release', ext: 'aar')
    api (name: 'newcall_dc_aidl-release', ext: 'aar')
    // http stack for ims dc.
    embed(name: 'newcall_dc_httpstack-release', ext: 'aar')
    api(name: 'newcall_dc_httpstack-release', ext: 'aar')
    // dc 涂鸦
    embed (name: 'newcall_sketch-release', ext: 'aar')
    api (name: 'newcall_sketch-release', ext: 'aar')
//    // dc aidl接口
//    embed project(':newcall_dc:newcall_dc_aidl')
//    api project(':newcall_dc:newcall_dc_aidl')
//    // dc http协议栈
//    embed project(':newcall_dc:newcall_dc_httpstack')
//    api project(':newcall_dc:newcall_dc_httpstack')
//    // dc 涂鸦
//    embed project(':newcall_dc:newcall_sketch')
//    api project(':newcall_dc:newcall_sketch')
    // glide
    implementation "com.github.bumptech.glide:glide:4.12.0"
    // PictureSelector
    implementation 'io.github.lucksiege:pictureselector:v3.10.7'
    implementation 'io.github.lucksiege:camerax:v3.10.7'
}

// maven-publish multi-flavor 配置
if (android.productFlavors.size() > 0) {
    android.libraryVariants.all { variant ->
        if (variant.name.toLowerCase().contains("debug")) {
            return
        }
        def bundleTask = tasks["bundle${variant.name.capitalize()}Aar"]

        artifacts {
            archives(bundleTask.archivePath) {
                classifier variant.flavorName
                builtBy bundleTask
                name = project.name
            }
        }
    }
}

// 通过 jitpack 引用AwesomeNewCall
//allprojects {
//    repositories {
//        google()
//        jcenter()
//        maven { url "https://jitpack.io" }
//    }
//    dependencies {
//        implementation 'com.github.NewCallTeam:AwesomeNewCall:master-SNAPSHOT'
//    }
//}